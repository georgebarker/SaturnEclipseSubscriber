buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
        springDependencyManagementVersion =  '1.0.6.RELEASE'
    }
    repositories {
    mavenCentral()

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
        classpath('commons-httpclient:commons-httpclient:3.1')
        
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
       dirs 'lib'
   	}
}

configurations {
    providedRuntime    
    jaxb
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("commons-httpclient:commons-httpclient:3.1")
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") 
    
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.4-1'
    
    compile name: '2wayfilter-1.2'
}


eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
    }
}

wrapper {
    gradleVersion = '4.10'

    def jvmOpts = "-Xmx4000m"
    inputs.property("jvmOpts", jvmOpts)
    doLast {
        def optsEnvVar = "DEFAULT_JVM_OPTS"
        scriptFile.write scriptFile.text.replace("$optsEnvVar=\"\"", "$optsEnvVar=\"$jvmOpts\"")
        batchScript.write batchScript.text.replace("set $optsEnvVar=", "set $optsEnvVar=$jvmOpts")
    }
}

test {
	maxHeapSize = "4000m"
	testLogging {
		systemProperties 'logback.configurationFile' : new File('src/main/resources/logback.xml').absolutePath		
		showStandardStreams = true		
	}
}
bootRun {
	maxHeapSize = "4000m"
}

bootJar {
	baseName = 'datex2-client-toolkit'
	version = '1.0.0'
}

task jaxb {
	doLast {
		ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask',  classpath: configurations.jaxb.asPath)
		ant.xjc(destdir: "src/main/java", package: "com.se.datex2.schema") {
	    	schema(dir: "src/main/webapp/xsd/", includes: 'DATEXIISchema_2_2_0withdefinitions.xsd')
		}
	}
}
eclipseClasspath.dependsOn(jaxb)

